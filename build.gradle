buildscript {
    repositories {
        jcenter()
        maven {
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'

targetCompatibility = 8
sourceCompatibility = 8

version = "${mc_version}-${mod_version}"
group = "com.oortcloud.hungryanimals"
archivesBaseName = "hungryanimals"

minecraft {
    version = "${mc_version}-14.23.5.2838"
    runDir = "run"

    replace "@VERSION@", project.version
    replaceIn "References.java"

    mappings = "snapshot_20171003"
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

repositories {
    maven { // TOP
        name 'tterrag maven'
        url "http://maven.tterrag.com/"
    }
    maven {
        name = "COFH, Codechicken"
        url "http://maven.covers1624.net/"
    }
    maven {
        // location of the maven that hosts JEI files
        name = "Progwml6 maven"
        url = "http://dvs1.progwml6.com/files/maven"
    }
    maven {
        // location of a maven mirror for JEI files, as a fallback
        name = "ModMaven"
        url = "modmaven.k-4u.nl"
    }
}

dependencies {
    deobfCompile "mcjty.theoneprobe:TheOneProbe-1.12:1.12-1.4.19-11"
    // compile against the JEI API but do not include it at runtime
    deobfProvided "mezz.jei:jei_${mc_version}:${jei_version}:api"
    // at runtime, use the full JEI jar
    runtime "mezz.jei:jei_${mc_version}:${jei_version}"

    // JUnit
    compile "junit:junit:4.11"
}

ext.copyConfigsOneAnimal = { root, dirs, vanilla, types, genders ->
    dirs.each { dir ->
        types.each { type ->
            genders.each { gender ->
                copy {
                    from root+dir+'minecraft'
                    into root+dir+'animania'
                    include '**/'+vanilla+'.json'
                    rename { String fileName ->
                        fileName.replace(vanilla, gender+'_'+type)
                    }
                }
            }
        }
    }
}

task copyConfigs() {
    def root = 'src/main/resources/assets/hungryanimals/config/'
    def dirs = ["ais/", "attributes/", "food_preferences/block", "food_preferences/entity", "food_preferences/fluid",
                "food_preferences/item"]
    /*
    def dirs = ["ais/", "attributes/", "food_preferences/block", "food_preferences/entity",
                "food_preferences/fluid", "food_preferences/item", "loot_tables/", "productions/"]
     */
    def inputs = [
            ['pig',['duroc', 'hampshire', 'large_black', 'large_white', 'old_spot', 'yorkshire'],['sow', 'hog', 'piglet']],
            ['cow',['angus', 'friesian', 'hereford', 'holstein', 'jersey', 'mooshroom', 'highland', 'longhorn'],['cow', 'bull', 'calf']],
            ['chicken',['leghorn', 'orpington', 'plymouth_rock', 'rhode_island_red', 'wyandotte'],['hen','rooster','chick']],
            ['rabbit',['chinchilla','cottontail','dutch','havana','jack','lop','new_zealand','rex'],['doe','buck','kit']],
            ['sheep',['dorper','dorset','friesian','merino','jacob','suffolk'],['ewe','ram','lamb']]
    ]

    inputs.each { input ->
        copyConfigsOneAnimal(root, dirs, input[0], input[1], input[2])
    }
}



